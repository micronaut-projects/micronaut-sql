plugins {
    id 'io.micronaut.build.internal.sql-module'
}

sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api project(":jdbc")
    api(libs.micronaut.aop)
    api(libs.micronaut.inject)
    api(libs.jdbi3.core) {
        exclude group:'com.github.ben-manes.caffeine', module:'caffeine'
    }
    compileOnly(libs.h2)
    compileOnly(libs.micronaut.data.tx)
    compileOnly(libs.spring.jdbc)
    runtimeOnly(libs.micronaut.cache.caffeine)
    runtimeOnly "com.github.ben-manes.caffeine:caffeine:2.9.1", {
        version {
            strictly("2.9.1")
        }
    }

    testRuntimeOnly project(":jdbc-hikari")
    testRuntimeOnly(libs.h2)
    testRuntimeOnly(libs.spring.jdbc)

    testAnnotationProcessor(libs.micronaut.inject.java)
    testAnnotationProcessor(libs.micronaut.spring.annotation)
    testImplementation(libs.micronaut.spring)
    testImplementation(libs.micronaut.http.server.netty)
    testImplementation(libs.micronaut.http.client)
    testImplementation(libs.micronaut.management)

    testImplementation(libs.micronaut.micrometer.core)

    testImplementation(libs.micronaut.cache.core)
    testImplementation(libs.jdbi3.sqlobject)

    txTestRuntimeOnly project(":jdbc-tomcat")
    txTestRuntimeOnly "com.h2database:h2:1.4.200"
    txTestAnnotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
    txTestAnnotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    txTestImplementation "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"
    txTestImplementation("io.micronaut:micronaut-inject-groovy")
    txTestRuntimeOnly "ch.qos.logback:logback-classic:1.2.11"
    txTestImplementation(libs.jdbi3.sqlobject)
    txTestImplementation("org.spockframework:spock-core:${spockVersion}") {
        exclude module:'groovy-all'
    }
    txTestImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
}


task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest

project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
