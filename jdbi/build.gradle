sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
    testCompileOnly "io.micronaut:micronaut-inject-java"

    api project(":jdbc")
    api "io.micronaut:micronaut-aop:$micronautVersion"
    api "io.micronaut:micronaut-inject:$micronautVersion"
    api "org.jdbi:jdbi3-core:3.18.0"
    compileOnly "com.h2database:h2"
    compileOnly "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"
    compileOnly "org.springframework:spring-jdbc:$springVersion"

    testRuntimeOnly project(":jdbc-hikari")
    testRuntimeOnly "com.h2database:h2:1.4.200"
    testRuntimeOnly "org.springframework:spring-jdbc:$springVersion"

    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut.spring:micronaut-spring"
    testImplementation "io.micronaut:micronaut-http-server-netty"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut:micronaut-management"
    
    testImplementation "io.micronaut.micrometer:micronaut-micrometer-core"
   
    testImplementation "io.micronaut.cache:micronaut-cache-core"
    testImplementation "org.jdbi:jdbi3-sqlobject:3.17.0"
    testImplementation("org.spockframework:spock-core:${spockVersion}") {
        exclude module:'groovy-all'
    }
    testImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"

    txTestRuntimeOnly project(":jdbc-tomcat")
    txTestRuntimeOnly "com.h2database:h2:1.4.200"
    txTestAnnotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
    txTestAnnotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    txTestImplementation "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"
    txTestImplementation("io.micronaut:micronaut-inject-groovy")
    txTestRuntimeOnly "ch.qos.logback:logback-classic:1.2.3"
    txTestImplementation "org.jdbi:jdbi3-sqlobject:3.17.0"
    txTestImplementation("org.spockframework:spock-core:${spockVersion}") {
        exclude module:'groovy-all'
    }
    txTestImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
}


task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest

project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
