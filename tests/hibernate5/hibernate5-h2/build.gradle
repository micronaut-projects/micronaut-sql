plugins {
    id 'io.micronaut.build.internal.test-application'
}

dependencies {
    implementation projects.tests.hibernate5.hibernate5SyncCommon
    testImplementation projects.tests.hibernate5.hibernate5SyncCommonTests

    runtimeOnly libs.managed.h2
}

configurations {
    all*.exclude module: "byte-buddy"
    all*.exclude module: "javassist"
}

graalvmNative {
    binaries {
        main {
            buildArgs("--report-unsupported-elements-at-runtime")
        }
    }
}

// Skip on GraalVM Java11
project.afterEvaluate {
    tasks.named("testNativeImage") {
        onlyIf { isGraal() && JavaVersion.current() != JavaVersion.VERSION_11 }
    }
    tasks.named("nativeCompile") {
        onlyIf { isGraal() && JavaVersion.current() != JavaVersion.VERSION_11 }
    }
}


boolean isGraal() {
    for (String prop : ["jvmci.Compiler", "java.vendor.version"]) {
        String vv = System.getProperty(prop)
        if (vv != null && vv.toLowerCase(Locale.ENGLISH).contains("graal")) {
            return true
        }
    }
    return false
}
