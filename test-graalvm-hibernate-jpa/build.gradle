plugins {
    id 'io.micronaut.application' apply false
    id 'io.micronaut.library' apply false
}

subprojects { Project subproject ->

    apply plugin: "java"
    apply plugin: "groovy"

    group "io.micronaut.example"

    repositories {
        mavenCentral()
    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    configurations {
        all*.exclude module: "byte-buddy"
        all*.exclude module: "javassist"
    }

    if (subproject.getName().startsWith("common")) {
        apply plugin: 'io.micronaut.library'

        micronaut {
            version project.ext.micronautVersion
        }

        return
    }

    apply plugin: 'io.micronaut.application'

    micronaut {
        version project.ext.micronautVersion
        runtime "netty"
        testRuntime "spock"
    }

    dependencies {
        runtimeOnly project(":jdbc-hikari")
        runtimeOnly "ch.qos.logback:logback-classic"

        implementation "io.micronaut:micronaut-http-client"
        implementation project(":test-graalvm-hibernate-jpa:common")
        testImplementation project(":test-graalvm-hibernate-jpa:common-tests")

        testImplementation "io.micronaut.test:micronaut-test-core:$micronautTestVersion"
        testImplementation platform("org.testcontainers:testcontainers-bom:$testContainersVersion")
        testImplementation "org.testcontainers:spock"
        testImplementation "org.testcontainers:jdbc"
    }
    mainClassName = "example.Application"
}

project("common") {
    dependencies {
        implementation 'javax.inject:javax.inject:1'
        implementation 'jakarta.inject:jakarta.inject-api:2.0.0'
        implementation "io.micronaut:micronaut-http-client"
        annotationProcessor "io.micronaut:micronaut-graal"
        implementation project(":hibernate-jpa")
    }
}
project("common-tests") {
    dependencies {
        implementation "io.micronaut:micronaut-http-client"
        implementation 'jakarta.inject:jakarta.inject-api:2.0.0'
        implementation "io.micronaut.test:micronaut-test-core:$micronautTestVersion"
        implementation platform("org.testcontainers:testcontainers-bom:$testContainersVersion")
        implementation "org.testcontainers:spock"
        implementation "org.testcontainers:jdbc"
    }
}
project("h2") {
    dependencies {
        runtimeOnly "com.h2database:h2"
    }
    nativeImage {
        args("--report-unsupported-elements-at-runtime")
    }
    // Skip on GraalVM Java11
    tasks.findByName("testNativeImage").onlyIf { !isGraal() || JavaVersion.current() != JavaVersion.VERSION_11 }
    tasks.findByName("nativeImage").onlyIf { !isGraal() || JavaVersion.current() != JavaVersion.VERSION_11 }
}
project("mssql") {
    dependencies {
        runtimeOnly "com.microsoft.sqlserver:mssql-jdbc:$mssqlDriverVersion"
        testImplementation "org.testcontainers:mssqlserver"
    }
    nativeImage {
        args("--report-unsupported-elements-at-runtime")
        args("-H:+AddAllCharsets")
    }
}
project("postgres") {
    dependencies {
        runtimeOnly "org.postgresql:postgresql:$postgresDriverVersion"
        testImplementation "org.testcontainers:postgresql"
    }
}
project("oracle") {
    dependencies {
        runtimeOnly "com.oracle.database.jdbc:ojdbc8:$ojdbcVersion"
        testImplementation "org.testcontainers:oracle-xe"
    }
}
project("mysql") {
    dependencies {
        runtimeOnly "mysql:mysql-connector-java:$mysqlDriverVersion"
        testImplementation "org.testcontainers:mysql"
    }
}
project("mariadb") {
    dependencies {
        runtimeOnly "org.mariadb.jdbc:mariadb-java-client:$mariadbDriverVersion"
        testImplementation "org.testcontainers:mariadb"
    }
}

boolean isGraal() {
    for (String prop : ["jvmci.Compiler", "java.vendor.version"]) {
        String vv = System.getProperty(prop)
        if (vv != null && vv.toLowerCase(Locale.ENGLISH).contains("graal")) {
            return true
        }
    }
    return false
}