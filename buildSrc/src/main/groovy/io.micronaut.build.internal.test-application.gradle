plugins {
    id 'io.micronaut.build.internal.testing-base'
    id 'io.micronaut.application'
}

micronaut {
    version project.ext.micronautVersion
    runtime "netty"
    testRuntime "spock"
}

configurations.testCompileClasspath {
    resolutionStrategy.eachDependency { details ->
        if (details.requested.group == 'org.codehaus.groovy') {
            details.useTarget "org.apache.groovy:$details.requested.name:${libs.versions.groovy.get()}"
        }
    }
}

dependencies {
    runtimeOnly mn.logback.classic
    implementation mn.micronaut.http.client
    testImplementation mnTest.micronaut.test.core
    testImplementation libs.testcontainers.bom
    testImplementation libs.testcontainers.spock
    testImplementation libs.testcontainers.jdbc
}

application {
    mainClass = "example.Application"
}

// TODO: Re-enable native tests once we have Micronaut data for 4.0.0
project.afterEvaluate {
    tasks.named("testNativeImage") {
        onlyIf { false }
    }
    tasks.named("nativeCompile") {
        onlyIf { false }
    }
}

graalvmNative {
    binaries {
        main {
            buildArgs("--report-unsupported-elements-at-runtime")
        }
    }
}
