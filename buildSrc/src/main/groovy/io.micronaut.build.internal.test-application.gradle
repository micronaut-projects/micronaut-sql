plugins {
    id 'io.micronaut.build.internal.testing-base'
    id 'io.micronaut.application'
    id 'io.micronaut.test-resources'
}

micronaut {
    version project.ext.micronautVersion
    runtime "netty"
    testRuntime "junit"
    testResources {
        clientTimeout = 180
    }
}

dependencies {
    runtimeOnly mn.logback.classic
    implementation mn.micronaut.http.client
    testImplementation mnTest.micronaut.test.core
    testImplementation libs.testcontainers.bom
    testImplementation libs.testcontainers.jdbc
}

application {
    mainClass = "example.Application"
}

tasks.named("check") { task ->
    def graal = ["jvmci.Compiler", "java.vendor.version", "java.vendor"].any {
        println "$it ${System.getProperty(it)}"
        System.getProperty(it)?.toLowerCase(Locale.ENGLISH)?.contains("graal")
    }
    if (graal) {
        task.dependsOn("nativeTest")
    }
}

graalvmNative {
    toolchainDetection = false
    binaries {
        all {
            buildArgs.add("--report-unsupported-elements-at-runtime")
        }
    }
    metadataRepository {
        enabled = true
    }
}
