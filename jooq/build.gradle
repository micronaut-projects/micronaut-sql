plugins {
    id 'io.micronaut.build.internal.sql-module'
}

sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor(libs.micronaut.graal)

    // needed because otherwise IntelliJ integration broken
    compileOnly(libs.micronaut.inject.java)
    compileOnly(libs.graal)
    testCompileOnly(libs.micronaut.inject.java)

    // needed for native image (adding @Entity annotation to UpdatableRecords for reflection registration)
    api(libs.jakarta.persistence.api)

    api project(":jdbc")
    api(libs.micronaut.inject)
    api(libs.managed.jooq)

    compileOnly(libs.spring.jdbc)
    compileOnly(libs.micronaut.data.tx)
    compileOnly(libs.sfm.reflect)
    compileOnly(libs.jackson.databind)
    compileOnly(libs.micronaut.json.core)

    testRuntimeOnly project(":jdbc-tomcat")
    testRuntimeOnly(libs.h2)
    testAnnotationProcessor(libs.micronaut.inject.java)
    testAnnotationProcessor(libs.micronaut.spring.annotation)
    testImplementation(libs.micronaut.spring)
    testImplementation(libs.micronaut.http.server.netty)
    testImplementation(libs.micronaut.http.client)
    testImplementation(libs.micronaut.management)

    testImplementation(libs.micronaut.micrometer.core)
    testImplementation(libs.spring.jdbc)
    testImplementation(libs.micronaut.cache.core)

    txTestRuntimeOnly project(":jdbc-tomcat")
    txTestRuntimeOnly "com.h2database:h2:1.4.200"
    txTestAnnotationProcessor "io.micronaut:micronaut-inject-java:$micronautVersion"
    txTestAnnotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    txTestImplementation "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"
    txTestImplementation("io.micronaut:micronaut-inject-groovy")
    txTestImplementation("org.spockframework:spock-core:${spockVersion}") {
        exclude module:'groovy-all'
    }
    txTestImplementation "io.micronaut.test:micronaut-test-spock:$micronautTestVersion"
}

task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest
project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
