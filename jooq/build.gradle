sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor "io.micronaut:micronaut-inject-java"
    annotationProcessor "io.micronaut.docs:micronaut-docs-asciidoc-config-props:$micronautDocsVersion"
    // needed because otherwise IntelliJ integration broken
    compileOnly "io.micronaut:micronaut-inject-java"
    testCompileOnly "io.micronaut:micronaut-inject-java"

    api "io.micronaut:micronaut-jdbc:$micronautVersion"
    api "io.micronaut:micronaut-inject:$micronautVersion"
    api "org.jooq:jooq:3.13.0"
    compileOnly "org.springframework:spring-jdbc:$springVersion"
    compileOnly "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"

    testRuntime project(":jdbc-tomcat")
    testRuntime "com.h2database:h2"
    testAnnotationProcessor "io.micronaut:micronaut-inject-java"
    testImplementation "io.micronaut:micronaut-spring"
    testImplementation "io.micronaut:micronaut-http-server-netty"
    testImplementation "io.micronaut:micronaut-http-client"
    testImplementation "io.micronaut:micronaut-management"
    testImplementation "io.micronaut.configuration:micronaut-micrometer-core"
    testImplementation "org.springframework:spring-jdbc:$springVersion"

    txTestRuntime project(":jdbc-tomcat")
    txTestRuntime "com.h2database:h2"
    txTestAnnotationProcessor "io.micronaut:micronaut-inject-java"
    txTestAnnotationProcessor "io.micronaut.data:micronaut-data-processor:$micronautDataVersion"
    txTestImplementation "io.micronaut.data:micronaut-data-tx:$micronautDataVersion"
    txTestImplementation("io.micronaut:micronaut-inject-groovy")
    txTestImplementation("org.spockframework:spock-core:${spockVersion}")
    txTestRuntime "ch.qos.logback:logback-classic:1.2.3"
}

task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest
