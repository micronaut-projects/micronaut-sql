plugins {
    id 'io.micronaut.build.internal.sql-module'
}

sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor(libs.micronaut.graal)

    // needed because otherwise IntelliJ integration broken
    compileOnly(libs.micronaut.inject.java)
    compileOnly(libs.graal)
    testCompileOnly(libs.micronaut.inject.java)

    // needed for native image (adding @Entity annotation to UpdatableRecords for reflection registration)
    api(libs.jakarta.persistence.api)

    api project(":jdbc")
    api(libs.micronaut.inject)
    api(libs.managed.jooq)

    compileOnly(libs.spring.jdbc)
    compileOnly(libs.micronaut.data.tx)
    compileOnly(libs.sfm.reflect)
    compileOnly(libs.jackson.databind)
    compileOnly(libs.micronaut.json.core)

    testRuntimeOnly project(":jdbc-tomcat")
    testRuntimeOnly(libs.h2)
    testAnnotationProcessor(libs.micronaut.inject.java)
    testAnnotationProcessor(libs.micronaut.spring.annotation)
    testImplementation(libs.micronaut.spring)
    testImplementation(libs.micronaut.http.server.netty)
    testImplementation(libs.micronaut.http.client)
    testImplementation(libs.micronaut.management)

    testImplementation(libs.micronaut.micrometer.core)
    testImplementation(libs.spring.jdbc)
    testImplementation(libs.micronaut.cache.core)

    txTestAnnotationProcessor(platform(libs.micronaut.bom))
    txTestImplementation(platform(libs.micronaut.bom))
    txTestRuntimeOnly project(":jdbc-tomcat")
    txTestRuntimeOnly(libs.h2)
    txTestAnnotationProcessor(libs.micronaut.inject.java)
    txTestAnnotationProcessor(libs.micronaut.data.processor)
    txTestImplementation(libs.micronaut.data.tx)
    txTestImplementation(libs.micronaut.inject.groovy)
    txTestImplementation(libs.spock.core) {
        exclude module:'groovy-all'
    }
    txTestImplementation(libs.micronaut.test.spock)
}

task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest
project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
