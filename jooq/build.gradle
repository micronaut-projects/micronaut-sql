plugins {
    id 'io.micronaut.build.internal.sql-module'
}

sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor(mn.graal)

    // needed because otherwise IntelliJ integration broken
    compileOnly(mn.micronaut.inject.java)
    compileOnly(mn.graal)
    testCompileOnly(mn.micronaut.inject.java)

    // needed for native image (adding @Entity annotation to UpdatableRecords for reflection registration)
    api(libs.jakarta.persistence.api)

    api projects.jdbc
    api(mn.micronaut.inject)
    api(libs.managed.jooq)

    compileOnly(libs.spring.jdbc)
    compileOnly(mn.micronaut.data.tx)
    compileOnly(libs.sfm.reflect)
    compileOnly(mn.jackson.databind)
    compileOnly(mn.micronaut.json.core)

    testRuntimeOnly projects.jdbcTomcat
    testRuntimeOnly(libs.managed.h2)
    testAnnotationProcessor(mn.micronaut.inject.java)
    testAnnotationProcessor(mn.micronaut.spring.annotation)
    testImplementation(mn.micronaut.spring)
    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(mn.micronaut.http.client)
    testImplementation(mn.micronaut.management)

    testImplementation(mn.micronaut.micrometer.core)
    testImplementation(libs.spring.jdbc)
    testImplementation(mn.micronaut.cache.core)
    txTestAnnotationProcessor(platform(mn.micronaut.bom))
    txTestImplementation(platform(mn.micronaut.bom))
    txTestRuntimeOnly projects.jdbcTomcat
    txTestRuntimeOnly(libs.managed.h2)
    txTestAnnotationProcessor(mn.micronaut.inject.java)
    txTestAnnotationProcessor(mn.micronaut.data.processor)
    txTestImplementation(mn.micronaut.data.tx)
    txTestImplementation(mn.micronaut.inject.groovy)
    txTestImplementation(mn.micronaut.test.spock)
}

task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest
project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
