plugins {
    id 'io.micronaut.build.internal.sql-module'
}

sourceSets {
    txTest {
        compileClasspath += main.output
        runtimeClasspath += main.output
    }
}

configurations {
    txTestImplementation.extendsFrom implementation
    txTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    annotationProcessor(mn.micronaut.graal)

    // needed because otherwise IntelliJ integration broken
    compileOnly(mn.micronaut.inject.java)
    testCompileOnly(mn.micronaut.inject.java)

    // needed for native image (adding @Entity annotation to UpdatableRecords for reflection registration)
    api(libs.managed.jakarta.persistence.api)

    api projects.micronautJdbc
    api(mn.micronaut.inject)
    api(libs.managed.jooq)

    compileOnly(mnSpring.spring.jdbc)
    compileOnly(mnData.micronaut.data.tx)
    compileOnly(mnData.micronaut.data.tx.jdbc)
    compileOnly(libs.sfm.reflect)
    compileOnly(mn.jackson.databind)
    compileOnly(mn.micronaut.json.core)

    testRuntimeOnly projects.micronautJdbcTomcat
    testRuntimeOnly(libs.managed.h2)
    testAnnotationProcessor(mn.micronaut.inject.java)
    testAnnotationProcessor(mnSpring.micronaut.spring.annotation)
    testImplementation(mnSpring.micronaut.spring)
    testImplementation(mn.micronaut.http.server.netty)
    testImplementation(mn.micronaut.http.client)
    testImplementation(mn.micronaut.management)

    testImplementation(mnMicrometer.micronaut.micrometer.core)
    testImplementation(mnSpring.spring.jdbc)
    testImplementation(mnCache.micronaut.cache.core)
    txTestRuntimeOnly projects.micronautJdbcTomcat
    txTestRuntimeOnly(libs.managed.h2)
    txTestAnnotationProcessor(mn.micronaut.inject.java)
    txTestAnnotationProcessor(mnData.micronaut.data.processor)
    txTestImplementation(mnData.micronaut.data.tx)
    txTestImplementation(mnData.micronaut.data.tx.jdbc)
    txTestImplementation(mn.micronaut.inject.groovy)
    txTestImplementation(mnTest.micronaut.test.spock)
}

task txTest(type: Test) {
    testClassesDirs = sourceSets.txTest.output.classesDirs
    classpath = sourceSets.txTest.runtimeClasspath
    shouldRunAfter test
}
check.dependsOn txTest
project.afterEvaluate {
    checkstyleTxTest.enabled = false
}
